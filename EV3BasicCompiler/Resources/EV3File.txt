// ------------------------------------------ EV3 MODULE: EV3FILE --------------------------------------------

subcall EV3FILE.OPENWRITE       // SF
{
	IN_S filename 252
	OUT_F handle

	DATAS fullname 300
	DATA16 handle16

	STRINGS DUPLICATE filename fullname
	JR_EQ8 filename 47 absolutepath               // filename begins with a '/'
	STRINGS ADD '/home/root/lms2012/prjs/' filename fullname
absolutepath:
	FILE OPEN_WRITE fullname handle16

	MOVE16_F handle16 handle
}

subcall EV3FILE.OPENAPPEND       // SF
{
	IN_S filename 252
	OUT_F handle

	DATAS fullname 300
	DATA16 handle16

	STRINGS DUPLICATE filename fullname
	JR_EQ8 filename 47 absolutepath               // filename begins with a '/'
	STRINGS ADD '/home/root/lms2012/prjs/' filename fullname
absolutepath:
	FILE OPEN_APPEND fullname handle16

	MOVE16_F handle16 handle
}

subcall EV3FILE.OPENREAD       // SF
{
	IN_S filename 252
	OUT_F handle

	DATAS fullname 300
	DATA16 handle16
	DATA32 dummylength

	STRINGS DUPLICATE filename fullname
	JR_EQ8 filename 47 absolutepath               // filename begins with a '/'
	STRINGS ADD '/home/root/lms2012/prjs/' filename fullname
absolutepath:
	FILE OPEN_READ fullname handle16 dummylength

	MOVE16_F handle16 handle
}

subcall EV3FILE.CLOSE          // FV
{
	IN_F handle

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	MOVEF_16 handle handle16
	FILE CLOSE handle16
invalidhandle:
}

subcall EV3FILE.WRITELINE     // FSV
{
	IN_F handle
	IN_S text 252

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	MOVEF_16 handle handle16
	FILE WRITE_TEXT handle16 6 text
invalidhandle:
}

subcall EV3FILE.WRITEBYTE     // FFV
{
	IN_F handle
	IN_F byte

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	DATA8 byte8
	MOVEF_16 handle handle16
	MOVEF_8 byte byte8
	FILE WRITE_BYTES handle16 1 byte8
invalidhandle:
}

subcall EV3FILE.READLINE     // FS
{
	IN_F handle
	OUT_S text 252

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	MOVEF_16 handle handle16
	FILE READ_TEXT handle16 6 127 text
	RETURN

invalidhandle:
	STRINGS DUPLICATE '' text
}

subcall EV3FILE.READBYTE     // FF
{
	IN_F handle
	OUT_F byte

	MOVEF_F 0.0 byte

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	DATA8 byte8
	MOVEF_16 handle handle16
	FILE READ_BYTES handle16 1 byte8
	MOVE8_F byte8 byte

invalidhandle:
}

subcall EV3FILE.READNUMBERARRAY    // FFA
{
	IN_F handle
	IN_F size
	IN_16 a

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	DATA8 byte8
	MOVEF_16 handle handle16

	DATA32 size32
	MOVEF_32 size size32
	JR_LTEQ32 size32 0 empty

	ARRAY RESIZE a size32
	ARRAY FILL a 0.0

	MUL32 size32 4 size32  // from here calculate all in bytes
	DATA32 offset          // position where to put data
	ARRAY8 buffer 4000     // buffer is big enough to hold 1000 float values at once
	MOVE32_32 0 offset     // byte offset into array
copyloop:
	JR_LTEQ32 size32 4000 lastchunk
    FILE READ_BYTES handle16 4000 buffer 
    ARRAY WRITE_CONTENT 1 a offset 4000 buffer
	ADD32 offset 4000 offset
	SUB32 size32 4000 size32
	JR copyloop

lastchunk:
	DATA16 size16
	MOVE32_16 size32 size16
	FILE READ_BYTES handle16 size16 buffer 
  	ARRAY WRITE_CONTENT 1 a offset size32 buffer

	RETURN

invalidhandle:
empty:
    ARRAY RESIZE a 0
}

subcall EV3FILE.WRITENUMBERARRAY    // FFAV
{
	IN_F handle
	IN_F size
	IN_16 a

	JR_LTF handle 1.0 invalidhandle
	JR_GTF handle 32767.0 invalidhandle
	DATA16 handle16
	DATA8 byte8
	MOVEF_16 handle handle16

	DATA32 size32
	MOVEF_32 size size32
	JR_LTEQ32 size32 0 empty

	DATA32 len          // real size of array
	ARRAY SIZE a len

	ARRAY8 buffer 4     // buffer for a single float

	DATA32 i            // loop counter
	MOVE32_32 0 i
copyloop:
	JR_GTEQ32 i size32 over
	JR_GTEQ32 i len outofbounds
	ARRAY_READ a i buffer
outofbounds:
	FILE WRITE_BYTES handle16 4 buffer

	ADD32 i 1 i
	JR copyloop

over:
invalidhandle:
empty:
	RETURN
}


subcall EV3FILE.CONVERTTONUMBER            // SF
{
	IN_S s 252
	OUT_F value

	STRINGS STRING_TO_VALUE s value
}

subcall EV3FILE.TABLELOOKUP   // SFFFF        EV3.NATIVECODE
{
	IN_S filename 252
	IN_F bytes_per_row
	IN_F row
	IN_F column
	OUT_F value

	DATA32 bpr
	DATA32 r
	DATA32 c
	MOVEF_32 bytes_per_row bpr
	MOVEF_32 row r
	MOVEF_32 column c

	DATAS command 400 
	DATAS response 252
	DATAS tmp 20

	STRINGS DUPLICATE 'tablelookup ' command
	JR_EQ8 filename 47 absolutepath               // filename begins with a '/'
	STRINGS DUPLICATE 'tablelookup /home/root/lms2012/prjs/' command
absolutepath:
	STRINGS ADD command filename command

	STRINGS NUMBER_FORMATTED bpr ' %d' 99 tmp
	STRINGS ADD command tmp command
	STRINGS NUMBER_FORMATTED r ' %d' 99 tmp
	STRINGS ADD command tmp command
	STRINGS NUMBER_FORMATTED c ' %d' 99 tmp
	STRINGS ADD command tmp command

	CALL EV3.NATIVECODE command value
}
